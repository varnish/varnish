varnishtest "Test vmod-tls ja3 fingerprint"

# Ported from Varnish Enterprise vmodtests/tls/test05.vtc

feature tls_1_3

shell {
cat >vtls.cfg <<EOF
  frontend = {
      host = "*"
      port = "0"
  }

  pem-file = "${topsrc}/etc/ca/bundle/example.com.pem"
  tls-protos = TLSv1.3
EOF
}

server s1 {
	rxreq
	txresp
} -start

varnish v1 -arg "-A ${tmpdir}/vtls.cfg" -vcl+backend {
	import tls;

	sub vcl_deliver {
		set resp.http.ja3 = tls.ja3();
	}
}

varnish v1 -cli "param.set tls_ja3 on"
varnish v1 -start

client c1 {
	tls_config {
		version = TLSv1.3
		ciphersuites = "TLS_AES_128_GCM_SHA256"
	}
	tls_handshake
	txreq
	rxresp
	# JA3 format per https://github.com/salesforce/ja3: five comma-separated
	# fields (SSLVersion,Cipher,SSLExtension,EllipticCurve,EllipticCurvePointFormat),
	# decimals with hyphens within fields; fields may be empty.
	expect resp.http.ja3 ~ {^\d+,(\d+(-\d+)*)?,(\d+(-\d+)*)?,(\d+(-\d+)*)?,(\d+(-\d+)*)?$}
} -run
