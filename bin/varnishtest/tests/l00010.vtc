varnishtest "fail/abandon"

feature cmd jq

server s1 {
	rxreq
	txresp
} -start

varnish v1 -vcl+backend {
	import std;

	backend black_hole none;

	sub vcl_recv {
		if (req.url == "/recv_fail") {
			return (fail);
		} else {
			return (pass);
		}
	}

	sub vcl_backend_fetch {
		set bereq.backend = s1;
		if (bereq.url == "/fetch_fail") {
			return (fail);
		} else if (bereq.url == "/fetch_abandon" || bereq.url == "/fetch_error") {
			set bereq.backend = black_hole;
			return (fetch);
		} else {
			return (fetch);
		}
	}

	sub vcl_backend_error {
		if (bereq.url == "/fetch_abandon") {
			return(abandon);
		}
	}
} -start

client c1 {
	txreq -url "/recv_fail"
	rxresp
} -run

client c1 {
	txreq -url "/fetch_fail"
	rxresp
} -run


client c1 {
	txreq -url "/fetch_abandon"
	rxresp
} -run

client c1 {
	txreq -url "/fetch_error"
	rxresp
} -run

# give some time for the logs to land (0.1s is overly generous)
delay 0.1

shell {
	# arguments: URL SIDE FIELD EXPECTED_VALUE
	t() {
		set -ex
		test "$(varnishlog-json -$2 -n ${v1_name} -d | jq -r "select(.req.url == \"$1\") | .$3" )" = "$4"
	}

	varnishlog-json -c -n ${v1_name} -d
	varnishlog-json -b -n ${v1_name} -d
	varnishlog-json -c -n ${v1_name} -d | jq >${tmpdir}/varnishlog-json-003c.out
	varnishlog-json -b -n ${v1_name} -d | jq >${tmpdir}/varnishlog-json-003b.out

	t /recv_fail c handling fail
	t /recv_fail c resp.reason "VCL failed"
	# Open-source change: return(fail) produces a VCL_Error record
	t /recv_fail c errors[0] "Failed from VCL"

	t /fetch_fail b handling fail
	t /fetch_fail b errors[0] "Failed from VCL"
	t /fetch_fail c handling synth
	t /fetch_fail c errors null
	t /fetch_fail c resp.reason "Service Unavailable"

	t /fetch_abandon b handling abandon
	t /fetch_abandon b errors[0] "No backend"
	t /fetch_abandon c handling synth
	t /fetch_abandon c errors null
	t /fetch_abandon c resp.reason "Service Unavailable"

	t /fetch_error b handling error
	t /fetch_error b errors[0] "No backend"
	t /fetch_error c handling pass
	t /fetch_error c errors null
	t /fetch_error c resp.reason "Backend fetch failed"
}
