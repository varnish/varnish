varnishtest "Test crash reload with TLS certs"

feature tls

shell {
cat >vtls.cfg <<EOF
  frontend = {
      host = "${localhost}"
      port = "0"
  }
  pem-file = "${topsrc}/etc/ca/bundle/example.com.pem"
EOF
}

varnish v1 -arg "-A ${tmpdir}/vtls.cfg" -vcl+backend {
    backend be none;
    sub vcl_recv { return (synth(200)); }
} -start

varnish v1 -cliok "param.set feature +no_coredump"
varnish v1 -cliok "param.set auto_restart on"

# Load cert and commit it
varnish v1 -cliok "tls.cert.load COMMITTED ${topsrc}/etc/ca/bundle/no-san.example.com.pem"
varnish v1 -cliok "tls.cert.commit"

# Load cert but don't commit it (staged)
varnish v1 -cliok "tls.cert.load STAGED ${topsrc}/etc/ca/bundle/cdnN.example.com.pem"

varnish v1 -cliok "tls.cert.list"

# Trigger panic
varnish v1 -clierr 400 "debug.panic.worker"
varnish v1 -wait-running

# Committed cert should still work
client c1 {
	tls_config {
		servername = no-san.example.com
	}
	tls_handshake
	expect tls.cert.subject == "no-san.example.com"
} -run

# Staged cert should be gone - falls back to default
client c2 {
	tls_config {
		servername = cdn01.example.com
	}
	tls_handshake
	expect tls.cert.subject == "example.com"
} -run

varnish v1 -cliok "panic.clear"
varnish v1 -expectexit 0x40
